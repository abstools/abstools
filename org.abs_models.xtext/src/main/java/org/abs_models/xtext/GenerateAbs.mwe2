module org.abs_models.xtext.GenerateAbs

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = "org.abs_models.xtext"
                rootPath = rootPath
                runtimeTest = {
                    // do not create AbsParsingTest.xtend
                    enabled = false
                }
                genericIde = {
                    enabled = true
                }
                mavenLayout = true
            }
            code = {
                encoding = "UTF-8"
                lineDelimiter = "\n"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
                preferXtendStubs = false
            }
        }
        language = StandardLanguage {
            name = "org.abs_models.xtext.Abs"
            fileExtensions = "abs"
            // https://stackoverflow.com/questions/35558548/xtext-turn-on-backtracking-correctly/35561986#35561986
            parserGenerator = {
                options = {
                    backtrack = true
                }
            }
            validator = {
                // composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
                // Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
                generateDeprecationValidation = true
            }
            serializer = {
                generateStub = true
            }
            junitSupport = {
                junitVersion = "5"
            }
        }
    }
}
