import org.jastadd.JastAddTask

plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // Apply the application plugin to add support for building an application
    id 'application'

    // JastAdd plugin documentation at https://github.com/jastadd/jastaddgradle
    id 'org.jastadd' version '1.13.3'
    id 'antlr'
    id "com.github.spotbugs" version "3.0.0"
}

application {
    mainClassName = 'org.abs_models.frontend.parser.Main'
}

java {
    withJavadocJar()
    withSourcesJar()
}

distributions {
    main { distributionBaseName = 'absc' }
}

dependencies {
    // https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_plugin_and_dependency_management
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation files('lib/choco-solver-2.1.1.jar')
    implementation 'org.eclipse.jdt:ecj:3.20.0'
    implementation 'org.apfloat:apfloat:1.9.1'
    // our code fails to compile with newer versions of sat4j; leaving this at
    // version 2.3.0 for now.
    implementation 'org.sat4j:org.sat4j.core:2.3.0'
    implementation 'org.sat4j:org.sat4j.pb:2.3.0'
    implementation 'org.sat4j:org.sat4j.maxsat:2.3.0'
    implementation files('lib/semisolver.jar')

    implementation 'info.picocli:picocli:4.1.4'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // Executes JUnit Jupiter (~JUnit 5) tests
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    // Executes JUnit4 tests
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.5.2")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.easymock:easymock:4.1'
    testImplementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    jastadd2 'org.jastadd:jastadd:2.3.4'
    antlr 'org.antlr:antlr4:4.7.2'
}

// JastAdd
task generateJastAddAST(type: JastAddTask) {
    description 'Processes the JastAdd AST.'
    outputDir = file("$buildDir/generated-src/jastadd/main")
    sources = fileTree(dir: 'src/main/java',
                       includes: ['**/*.ast', '**/*.jrag', '**/*.jadd'])
    options = [ '--rewrite=regular', '--visitCheck=false', '--debug',
               '--package=org.abs_models.frontend.ast']
}
sourceSets.main.java.srcDir "$buildDir/generated-src/jastadd/main"
compileJava.dependsOn 'generateJastAddAST'

// antlr
generateGrammarSource {
    arguments += [ '-package', 'org.abs_models.frontend.antlr.parser' ]
}
compileJava.dependsOn 'generateGrammarSource'

// erlang

// There's an erlang plugin at "id 'org.ccrusius.erlang' version
// '2.0.8'" but it re-downloads and compiles rebar after each "gradle
// clean", and we need to have an executable rebar in the source tree
// anyway since it is needed in the compiler itself, so we use a
// simple Exec task instead.

// task compileErlangBackend(type: org.ccrusius.erlang.tasks.Rebar) {
//     setRebarVersion '2.6.0'
//     setRebarTarget 'compile'
//     setDirectory 'src/main/resources/erlang/absmodel'
//     outputs.file('src/main/resources/erlang/absmodel/ebin/cog.beam')
// }

task compileErlangBackend(type: Exec, dependsOn: processResources) {
    description 'Compiles Erlang backend support files.'
    workingDir 'build/resources/main/erlang/absmodel'
    commandLine 'escript', '../bin/rebar', 'compile'
}
jar.dependsOn 'compileErlangBackend'

// jar
jar {
    // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Jar.html
    archiveBaseName='absfrontend'
    archiveAppendix=gitBranch()
    duplicatesStrategy='exclude'
    manifest {
        attributes 'Main-Class': 'org.abs_models.Absc',
            'Implementation-Title': 'ABS Frontend',
            'Implementation-Version': project.version.tokenize('-').first(),
            'Bundle-Version': project.version,
            // This is wrong, but we use it anyway for the detailed
            // version string (java.lang.Package only has methods
            // getVersion and getSpecificationVersion)
            'Specification-Version': project.version
    }
    from configurations.runtimeClasspath
        .findAll { it.name.endsWith('jar') }
        .collect { zipTree(it) }
    // Avoid problems with signed jar dependencies -- see
    // https://stackoverflow.com/a/34856095/564395
    excludes = [ 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA' ]
    reproducibleFileOrder=true
}

// this is necessary for the `absc` scripts
task plainJar(type: Copy, dependsOn: jar) {
    description 'Copies absfrontend.jar into its documented location.'
    from jar.archiveFile
    into 'dist'
    rename '(.*).jar', 'absfrontend.jar'
}
clean {
    delete 'dist/'
}
assemble.dependsOn plainJar

// `absc` script et al. -- copy to well-known location and instruct
// users to set their $PATH (see e.g. `frontend/README.md`,
// https://abstools.github.io/getting_started/local-install/)
task copyScripts(type: Copy) {
    description 'Copies absc scripts into their documented locations.'
    from('src/main/resources') {
        include 'bash/**'
        include 'win/**'
    }
    into 'bin/'
}
clean {
    delete 'bin/'
}
assemble.dependsOn copyScripts

task generateAutocomplete(type: JavaExec) {
    main = 'picocli.AutoComplete'
    classpath = sourceSets.main.runtimeClasspath
    args = [ '--force', '--completionScript', 'bin/bash/absc_completion.sh', 'org.abs_models.Absc' ]
}
assemble.dependsOn generateAutocomplete

// test
test {
    // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
    useJUnitPlatform()
    maxParallelForks=project.gradle.startParameter.maxWorkerCount

    // Allow dynamic exclusion of tests (based on https://blog.jdriven.com/2017/10/run-one-or-exclude-one-test-with-gradle/)
    // Usage: gradle test -PexcludeTests='**/ErlangModelApiTests*,**/ErlangExamplesTests*'
    if (project.hasProperty('excludeTests')) {
        project.property('excludeTests').split(',').each { 
            exclude it
        }
    }
}

spotbugs {
    // https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.FindBugsExtension.html
    ignoreFailures = true
    showProgress = true
    excludeFilter = file('config/findbugs/findbugs-jastadd-filter.xml')
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        // can only have one report type :(
        xml.enabled = false
        html.enabled = true
        emacs.enabled = false
    }
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.tags = [ 'apilevel', 'declaredat', 'aspect', 'ast', 'attribute',
                    'argument', 'astdecl', 'production' ]
}
