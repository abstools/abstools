module Hello;
import * from ABS.Meta;

interface Hello {
    String sayHello();
}
class HelloImp implements Hello {
    String sayHello() {
        return "Hello world";
    }
}

class Application() {
    Unit run() {
        Hello greeter = new local HelloImp();
	    while (True) {
            String message = greeter.sayHello();
            Unit u = println("\n*** " + message + " ***\n");
            this.switchLanguage();
	    }
    }

    Unit switchLanguage() {
        Runtime rt = new local Runtime();
        ProductLine pl = rt.getProductLine();
        List<Product> items = pl.getConfigurableProducts();

        Int i = 0;
        Unit u = println("Select Language:");
        while (i < length(items)) {
            Product item = nth(items, i);
            String pname = item.getName();
            u = println(intToString(i) + ": " + pname);
            i = i + 1;
        }
        String selection = readln();
        pl.configureProduct(nth(items, toInt(selection)));
    }
}

def Int toInt(String s) =
    case s {
        "0" => 0; "1" => 1; "2" => 2; "3" => 3; "4" => 4;
        "5" => 5; "6" => 6; "7" => 7; "8" => 8; "9" => 9;
    };

{
    new local Application();
}

//*************************************************************
delta En;
modifies class Hello.HelloImp {
    modifies String sayHello() {
        return "Hello World";
    }
}
//*************************************************************
delta Fr;
modifies class Hello.HelloImp {
    modifies String sayHello() {
        return "Bonjour tout le monde";
    }
}
//*************************************************************
delta De;
modifies class Hello.HelloImp {
    modifies String sayHello() {
        return "Hallo Welt";
    }
}
