== Software Product Line Engineering

ABS supports the development of software product lines with a set of language
constructs for defining system variants. Following a feature-oriented software
development approach, variants are described as sets of features.  Features
and their dependencies are specified in a feature model.  Each feature has its
corresponding ABS implementation. A feature's implementation is specified in
terms of the code modifications (i.e., additions and removals) that need to be
performed to a variant of the system that does not include that feature in
order to add it. This style of programming is called delta-oriented
programming. The ABS code modules that encapsulate feature implementations are
called delta modules (deltas).

ABS has currently experimental support for local product lines, which describe the 
variability of a single module and allow one to use multiple variants of this module in one model.
The following sections describe global product lines, local product lines are described in the very last section of this chapter.


=== Delta-Oriented Programming

ABS supports the delta-oriented programming model, an approach that aids the
development of a set of programs simultaneously from a single code base,
following the software product line engineering approach.  In delta-oriented
programming, features defined by a feature model are associated with code
modules that describe modifications to a core program.  In ABS, these modules
are called _delta modules_. Hence the implementation of a software product
line in ABS is divided into a _core_ and a set of delta modules.

The core consists of a set of ABS modules that implement a complete software
product of the corresponding software product line.  Delta modules (or
_deltas_ in short) describe how to change the core program to obtain new
products.  This includes adding new classes and interfaces, modifying existing
ones, or even removing some classes from the core.  Delta modules can also
modify the functional entities of an ABS program, that is, they can add and
modify data types and type synonyms, and add functions.

Deltas are applied to the core program by the ABS compiler front end. The
choice of which delta modules to apply depends on the selection of a set of
features, that is, a particular product of the SPL.  The role of the ABS
compiler front end is to translate textual ABS models into an internal
representation and check the models for syntax and semantic errors.  The role
of the back ends is to generate code for the models targeting some suitable
execution or simulation environment.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _DeltaDecl_      ::= | `delta` _SimpleTypeIdentifier_ [ `(` _DeltaParam_ { `,` _DeltaParam_ } `)` ] `;` +
 [ _ModuleAccess_ ] { _ModuleModifier_ }

| _DeltaParam_ ::= | _Type_ _SimpleIdentifier_ +
                   {vbar} _QualifiedTypeIdentifier_ _HasCondition_

| _HasCondition_ ::= | `hasField` FieldDecl +
               {vbar} `hasMethod` MethSig +
               {vbar} `hasInterface` TypeId

| _ModuleAccess_ ::= | `uses` _TypeId_ `;`

| _ModuleModifier_ ::= | `adds` _ClassDecl_ +
                    {vbar} `removes` `class` _TypeIdentifier_ `;` +
                    {vbar} `modifies` `class` _TypeIdentifier_ +
                    {nbsp} [`adds` _TypeIdentifier_ { `,` _TypeIdentifier_ } ] +
                    {nbsp} [`removes` _TypeIdentifier_ { `,` _TypeIdentifier_ } ] +
                    {nbsp} `{` { ClassModifier } `}` +
                    {vbar} `adds` _InterfaceDecl_ +
                    {vbar} `removes` `interface` _TypeIdentifier_ `;` +
                    {vbar} `modifies` `interface` _TypeIdentifier_ `{` { _InterfaceModifier_ } `}` +
                    {vbar} `adds` _FunctionDecl_ +
                    {vbar} `adds` _DataTypeDecl_ +
                    {vbar} `modifies` _DataTypeDecl_ +
                    {vbar} `adds` _TypeSynDecl_ +
                    {vbar} `modifies` _TypeSynDecl_ +
                    {vbar} `adds` _Import_ +
                    {vbar} `adds` _Export_
| _ClassModifier_ ::= | `adds` _FieldDecl_ +
               {vbar} `removes` _FieldDecl_ +
               {vbar} `adds` _Method_ +
               {vbar} `modifies` _Method_ +
               {vbar} `removes` _MethodSig_


| _InterfaceModifier_ ::= | `adds` _MethSig_ +
                    {vbar} `removes` _MethSig_

|====


The `DeltaDecl` clause specifies the syntax of delta modules, consisting of a
unique identifier, an optional list of delta parameters, an optional module
access directive, and a sequence of module modifiers.

The `ModuleAccess` clause specifies the semantics of unqualified identifiers
within a delta.  Any identifiers without namespace prefix will be resolved as
if they occurred in the module named in the `ModuleAccess` clause.  A delta
can always make additions and modifications in any module by fully qualifying
the `TypeName` within module modifiers.  New program elements (classes,
interfaces, etc.) with unqualified names are added to the module given in the
`uses` clause; it is an error to add new program elements with unqualified
names in a delta that does not have a `uses` clause.

While delta modeling supports a broad range of ways to modify an ABS model,
not all ABS program entities are modifiable.  These unsupported modifications
are listed here for completeness.  While these modifications could be easily
specified and implemented, we opted not to overload the language with features
that have not been regarded as necessary in practice:

Class parameters and init block::
Deltas currently do not support the modification of class parameter lists or
class init blocks.
Deltas::
currently only support adding functions, and adding and modifying data types
and type synonyms. Removal is not supported.
Modules::
Deltas currently do not support adding new modules or removing modules.
Imports and Exports::
While deltas do support the addition of import and export statements to
modules, they do not support their modification or removal.
Main block::
Deltas currently do not support the modification of the program's main block.


=== The Feature Model

Software variability is commonly expressed using features which can be present 
or absent from a product of the product line. Features are defined and organised in a 
feature model, which is essentially a set of logical constraints expressing 
the dependencies between features. Thus the feature model defines a set of 
legal feature combinations, which represent the set of valid software variants 
that can be built.

==== Specifying the Feature Model

The `FeatureModel` clause specifies a number of "orthogonal" root feature
models along with a number of extensions that specify additional constraints,
typically cross-tree dependencies.  Its grammar is as follows:

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _FeatureModel_ ::= | { `root` _FeatureDecl_ } { _FeatureExtension_ }

| _FeatureDecl_  ::= | _FName_ [ `{` [ _Group_ ] { _AttributeDecl_ } { _Constraint_ } `}` ]

| _FeatureExtension_ ::= | `extension` _FName_ `{` { _AttributeDecl_ } { _Constraint_ } `}`

| _Group_ ::= | `group` _Cardinality_ `{` [ `opt` ] _FeatureDecl_ { `,` [ `opt` ] _FeatureDecl_ } `}`

| _Cardinality_ ::= | `allof` {vbar} `oneof` {vbar} `[` _IntLiteral_ `..` _Limit_ `]`

| _AttributeDecl_ ::= | `Int` _AName_ `;` +
 {vbar}  `Int` _AName_ `in` `[` _Limit_ `..` _Limit_ `]` `;` +
 {vbar}  `Int` _AName_ `in` `{` _IntLiteral_ { `,` _IntLiteral_ } `}` `;` +
 {vbar} `Bool` _AName_ `;` +
 {vbar} `String` _AName_ `;`

| _Limit_ ::= | _IntLiteral_ {vbar} `*`

| _Constraint_ ::= | _Expr_ `;` +
  {vbar} `ifin` `:`  _Expr_ `;` +
  {vbar} `ifout` `:` _Expr_ `;` +
  {vbar} `require` `:` _FName_ `;` +
  {vbar} `exclude` `:` _FName_ `;`

| _Expr_ ::= | `True` +
  {vbar} `False` +
  {vbar} _IntLiteral_ +
  {vbar} _StringLiteral_ +
  {vbar} _FName_ +
  {vbar} _AName_ +
  {vbar} _FName_ `.` _AName_ +
  {vbar} _UnOp_ _Expr_ +
  {vbar} _Expr_ _BinOp_ _Expr_ +
  {vbar} `(` _Expr_ `)`
  
| _UnOp_ ::= | `!` {vbar} `-`

| _BinOp_ ::= | `{vbar}{vbar}` {vbar} `&&` {vbar} `\->` {vbar} `\<\->` {vbar} `==`
        {vbar} `!=` {vbar} `>`  {vbar} `<`  {vbar} `>=`  {vbar} `\<=`
        {vbar} `+`  {vbar} `-`  {vbar} `*`  {vbar} `/`   {vbar} `%`
|====

Attributes and values range over integers, strings or booleans.

The `FeatureDecl` clause specifies the details of a given feature, firstly by
giving it a name (`FName`), followed by a number of possibly optional
sub-features, the feature's attributes and any relevant constraints.

The `FeatureExtension` clause specifies additional constraints and attributes
for a feature, and if the extended feature has no children a group can also be
specified.  This is particularly useful for specifying constraints that do not
fit into the tree structure given by the root feature model.

Here is an example feature model for the `DeltaResourceExample` product line,
defining valid combinations of features and valid ranges of parameters for
cost, capacity and number of machines:

[source]
----
root Calculations {
  group oneof {
    Wordcount,
    Wordsearch
  }
}

root Resources {
  group oneof {
    NoCost,
    Cost { Int cost in [ 0 .. 10000 ] ; }
  }
}

root Deployments {
  group oneof {
    NoDeploymentScenario,
    UnlimitedMachines { Int capacity in [ 0 .. 10000 ] ; },
    LimitedMachines { Int capacity in [ 0 .. 10000 ] ;
      Int machinelimit in [ 0 .. 100 ] ; }
  }    
}
----

==== Feature Model Reflection

There is support for limited reflection on the feature model and configured
product in the module `ABS.Productline`.  The datatype `Feature` contains
constructors for all feature names.  The function `product_features` returns a
list of features contained in the current product, and `product_name` returns
the name of the product, or the empty string if no product was specified.

The following sample code shows the usage, assuming that product `Product` was
generated:

[source]
----
module Test;
import * from ABS.Productline;

{
  List<Feature> foo = product_features(); // => Cons(FeatureA, Cons(FeatureC, Nil)) 
  String name = product_name();           // => "Product"
}

productline Test;
features FeatureA, FeatureB, FeatureC;

product Product(FeatureA, FeatureC);
----



=== Software Product Lines and Products

A (software) product line is a set of software variants that can be built by selecting
any combination of features allowed by the feature model and applying the deltas 
that provide the implementation for those features to the core program. How features 
are associated with their implementation is defined in ABS with a _SPL configuration_.

An ABS _product_ is simply a set of features associated with a name.

==== Specifying the Product Line

The ABS configuration language links feature models, which describe the
structure of a SPL, to delta modules, which implement behavior.  The
configuration defines, for each selection of features satisfied by the product
selection, which delta modules should be applied to the core.  Furthermore, it
guides the code generation by ordering the application of the delta modules.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _Configuration_ ::= | `productline` _TypeId_ `;` _Features_ `;` { _DeltaClause_ }
| _Features_      ::= | `features` _FName_ { `,` _FName_ }
| _DeltaClause_   ::= | `delta` _DeltaSpec_ [ _AfterClause_ ] [ _WhenClause_ ] `;`
| _DeltaSpec_     ::= | _DeltaName_ [ `(` _DeltaParams_ `)` ]
| _DeltaName_     ::= | _TypeId_
| _DeltaParams_   ::= | _DeltaParam_ { `,` _DeltaParam_ }
| _DeltaParam_    ::= | _FName_ {vbar} _FName_ `.` _AName_
| _AfterClause_   ::= | `after` _DeltaName_ { `,` _DeltaName_ }
| _WhenClause_    ::= | `when` _AppCond_
| _AppCond_       ::= | _AppCond_ `&&` _AppCond_ +
                {vbar} _AppCond_ `{vbar}{vbar}` _AppCond_ +
                {vbar} `!` _AppCond_ +
                {vbar} `(` _AppCond_ `)` +
                {vbar} _FName_
|====


Features and delta modules are associated through _application conditions_ (a.k.a. _activation conditions_),
which are logical expressions over the set of features and attributes in a
feature model. The collection of applicable delta modules is given by the
application conditions that are true for a particular feature and attribute
selection. By not associating the delta modules directly with features, a
degree of flexibility is obtained.

Each delta clause has a `DeltaSpec`, specifying the name of a delta module
name and, optionally, a list of parameters; an `AfterClause`, specifying the
delta modules that the current delta must be applied after; and an application
condition `AppCond`, specifying an arbitrary predicate over the feature names
(`FName`) and attribute names (`AName`) in the feature model that describes
when the given delta module is applied.

[source]
----
productline DeltaResourceExample;
features Cost, NoCost, NoDeploymentScenario, UnlimitedMachines, LimitedMachines, Wordcount, Wordsearch;
delta DOccurrences when Wordsearch;
delta DFixedCost(Cost.cost) when Cost;
delta DUnboundedDeployment(UnlimitedMachines.capacity) when UnlimitedMachines;
delta DBoundedDeployment(LimitedMachines.capacity, LimitedMachines.machinelimit) when LimitedMachines;
----

==== Specifying Products

ABS allows the developer to name products that are of particular interest, in
order to easily refer to them later when the actual code needs to be
generated. A product definition states which features are to be included in
the product and sets attributes of those features to concrete values. In the simplest
case products are declared directly, by listing the features that they include.
It is also possible to declare products based on other products using 
_product expressions_. Product expressions use set-theoretic operations (union, 
intersection, complement) over products and sets of features.


[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _Selection_ ::= | `product` _TypeId_ ( `(` _FeatureSpecs_ `)` `;` {vbar} `=` _ProductExpr_ `;` )
| _ProductExpr_ ::= | `{` _FeatureSpecs_ `}` +
  {vbar}  _ProductExpr_ `&&` _ProductExpr_ +
  {vbar}  _ProductExpr_ `{vbar}{vbar}` _ProductExpr_ +
  {vbar}  _ProductExpr_ `-` _ProductExpr_ +
  {vbar}  _TypeId_ +
  {vbar}  `(` _ProductExpr_ `)`
| _FeatureSpecs_ ::= | _FeatureSpec_ { `,` _FeatureSpec_ }
| _FeatureSpec_ ::= | _FName_ [ _AttributeAssignments_ ]
| _AttributeAssignments_ ::= | `{` _AttributeAssignment_ { `,` _AttributeAssignment_ } `}`
| _AttributeAssignment_ ::= | _AName_ `=` _Literal_
|====

Here are some product definitions for the `DeltaResourceExample` product line:

[source]
----
product WordcountModel (Wordcount, NoCost, NoDeploymentScenario);
product WordcountFull (Wordcount, Cost{cost=10}, UnlimitedMachines{capacity=20});
product WordsearchFull (Wordsearch, Cost{cost=10}, UnlimitedMachines{capacity=20});
product WordsearchDemo (Wordsearch, Cost{cost=10}, LimitedMachines{capacity=20, machinelimit=2});
----

Here are some product definitions for the `CharityOrganizationExample` with `ProductExpr`:

[source]
----
product Org1 = SekolahBermainMatahari || {Continuous};
product Org2 = SekolahBermainMatahari || {Continuous, Automatic_Report};
product Org3 = SekolahBermainMatahari || PKPU;
product Org4 = SekolahBermainMatahari || PKPU || RamadhanForKids;
product Org5 = SekolahBermainMatahari || PKPU || RamadhanForKids || BeriBuku;
product Org6 = SekolahBermainMatahari && RamadhanForKids;
product Org7 = SekolahBermainMatahari && BeriBuku;
product Org8 = SekolahBermainMatahari - {Eventual};
product Org9 = SekolahBermainMatahari - {Eventual, Income};
product Org10 = SekolahBermainMatahari && RamadhanForKids || {Money, Item};
product Org11 = SekolahBermainMatahari && (RamadhanForKids || {Money, Item});

----

==== Checking the SPL

Because the number of variants in an SPL can be very large, checking them 
efficiently (e.g., to ensure that they are all well-typed) is challenging. 
Building each variant in order to type-check it is usually not feasible from a 
performance perspective. Instead, the ABS compiler employs a number of efficient 
consistency checks. These fall into two categories.

* _Family-based_ analysis steps operate on the SPL definition itself,
* Analysis steps operate on lightweight _abstractions_ of the SPL variants.

These checks are performed automatically upon compilation and help ensure that 
all variants defined by an SPL specified in ABS can be built and are well-typed 
ABS programs.







=== Local Product Lines
Local product lines are an experimental feature.

Local product lines describe the variability of a single module. When a variable class or interface of this module is referenced, then a variant of this module is generated.
For this generation, local deltas are used. A local delta is a special delta which modifies only classes and interfaces within its module. 
Similarly, there are local features, but instead of the feature modeling language for global variability, local product lines use simple boolean expressions.
To reference variable classes and interfaces, the type reference or `new` expression must contain a product of the module.

[source]
----
module M;
export I,C;

preproduct P = {g};

unique interface I { Unit m(); } //may not be modified
class C implements I { }


features f,g with (f || g) && !(f && g); //local features

//local deltas
delta D1; modifies C { adds Unit m() {println("1");} }
delta D2; modifies C { adds Unit m() {println("2");} }

delta D1 when f; delta D2 when g;

module N;
import * from M;
{
    I i = new C() with {f}; //references a variant of C
    I j = new C() with P; //references another variant of C
    i!m(); //prints "1"
    j!m(); //prints "2"
}
----

In variable modules, i.e., modules with local features, it is distinguished between `unique` and non-`unique`  classes (and interfaces). 
Only non-`unique` classes may be modified by local deltas.
A `unique` class may not reference on non-`unique` elements from any module without a `with` clause that specifies the product to use.
A non-`unique` class may not reference non-`unique` elements from other modules without a `with` clause that specifies the product to use.
It may reference non-`unique` classes within its own module.

In non-variable modules, all elements are implicitly `unique`.

One can use `preproduct` to give a name to a set of features from its own module.

Local variability is based on a syntactic operations, the above example results in the following model after rewriting but before type checking:
[source]
----
module M;
export *;
interface I { Unit m(); }

module M_f;
import * from M;
export C;

class C implements I { Unit m() {println("1");} }

module M_g;
import * from M;
export C;

class C implements I { Unit m() {println("2");} }

module N;
import * from M;
import * from M_f;
import * from M_g;
{
    M.I i = new M_f.D();
    M.I i = new M_g.D();
    i!m();
    j!m();
}
----

If both global and local product lines are used, then first the global product line is flattened and then the local ones. 
This may require to annotate classes added my global product lines with `unique`.
Local and global features are disjoint sets, global feature selection can, thus, not influence local feature selection.

==== Syntax
The syntax of modules is extended as follows.

 - Modules can define products using product declarations `preproduct _TypeId_ = { _Features_ }`. 

 - Modules can declare local features and a feature model using `features _Features_ with _BF_`, where `_BF_` is a boolean formula over the introduced features using the operators `||, &&, !`.

 - Modules can contain local deltas and local delta clauses.

 - Local deltas are identical to global deltas, except that (1) no `uses` module access clause can be used and (2) class and interfaces in module modifiers are not prefixed with the module. Local deltas must follow class and interface declarations.

 - Local delta clauses are identical to global delta clauses. Local delta clauses must follow local deltas.

The syntax of expressions, type references, class and interface declarations is extended as follows:

 - The `new` expression and type references can have a `with _PE_` clause, where `_PE_` is either a product name or a set of features.

- Class and interface declarations may be prefixed with `unique`.

