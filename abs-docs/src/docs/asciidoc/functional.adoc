== Expressions

This chapter specifies all ABS expressions.  Expressions are usually evaluated
for their value, although they can be used inside annotations as purely
syntactic constructs that do not need to follow standard evaluation rules.

ABS expressions can either be pure or have side effects. _Pure expressions_
only refer to entities in the functional and expression layer.  They can be
evaluated multiple times without influencing the execution of the imperative
and object oriented layers of ABS.  Pure expressions can be sub-expressions of
other expressions and can be used as function bodies.  _Side-effect
expressions_, on the other hand, refer to semantic entities “above” the
functional layer (objects and futures).  They are only legal “stand-alone”
(i.e., not as a sub-expression of another expression) and in certain places,
mostly in the right-hand side of an assignment.  This strict separation of
pure and side-effect expressions simplifies the language, for
example wrt. using expressions inside annotations and reasoning about
expressions in static analysis tools.


[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _Exp_ ::= | _PureExp_ {vbar} _EffExp_
| _PureExp_ ::= | _SimpleIdentifier_ +
 {vbar} `this` `.` _SimpleIdentifier_ +
 {vbar} `this` +
 {vbar} _Literal_ +
 {vbar} _TemplateString_ +
 {vbar} _LetExp_ +
 {vbar} _DataConstrExp_ +
 {vbar} _FnAppExp_ +
 {vbar} _FnAppListExp_ +
 {vbar} _ParFnAppExp_ +
 {vbar} _WhenExp_ +
 {vbar} _CaseExp_ +
 {vbar} _OperatorExp_ +
 {vbar} _TypeCheckExp_ +
 {vbar} _TypeCastExp_ +
 {vbar} `(` _PureExp_ `)`
| _EffExp_ ::= | _NewExp_ +
 {vbar} _SyncCall_ +
 {vbar} _AsyncCall_ +
 {vbar} _GetExp_ +
 {vbar} _AwaitExp_
|====

=== Literals

Literals, as defined in <<sec:literals>>, are expressions.

=== Template Strings

Template strings are strings allowing embedded expressions.  In
contrast to normal string literals, template strings can contain
linebreaks and other special characters.

A template string starts and ends with a backtick (```) character.  A
template string can contain zero or more pure expressions enclosed by
dollar signs (`$`).  These expressions will be replaced by their
string representation (as with the function `toString`) every time the
template string is evaluated.

The only characters that need to be escaped by a backslash (`\`) in a
template string are the backtick (```) and the dollar sign (`$`).  All
other characters, including line breaks and the backslash itself, do
not need to be specially treated when writing a template string.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _TemplateString_ ::= | ``` { ? _Valid String Character_ ? } ``` +
|                           | ```  { ? _Valid String Character_ ? } `$` _PureExp_ `$`
                              { { ? _Valid String Character_ ? } `$` _PureExp_ `$` }
                              { ? _Valid String Character_ ? } ```
|====

[source]
.Example
----
`Hello $name$!
The price is \$$price$.`
----
The result of this expression will be a string with two lines, and with the values of the local variables `name` and `price` in place.

=== Operator Expressions

ABS has a range of unary and binary operators working on pre-defined
datatypes.  All operators are pure (side effect-free).

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _OperatorExp_ ::= | _UnaryExp_ {vbar} _BinaryExp_
| _UnaryExp_ ::= | _UnaryOp_ _PureExp_
| _UnaryOp_ ::= | `!` {vbar} `-`
| _BinaryExp_ ::= | _PureExp_ _BinaryOp_ _PureExp_
| _BinaryOp_ ::= | `{vbar}{vbar}` {vbar} `&&` {vbar} `==` {vbar} `!=` {vbar} `<` {vbar} `\<=` {vbar} `>` {vbar} `>=` {vbar} `+` {vbar} `-` {vbar} `*` {vbar} `/` {vbar} `%`
|====


The following table describes the meaning as well as the associativity and the
precedence of the different operators. The list is sorted from low precedence
to high precedence.

.ABS Operators
[options="header"]
|=======================
|Expression    | Meaning                  |Associativity |Argument types | Result type
| `e1 \|\| e2` | logical or               |left          | `Bool`     | `Bool`
| `e1 && e2`   | logical and              |left          | `Bool`     | `Bool`
| `e1 == e2`   | equality                 |left          | compatible | `Bool`
| `e1 != e2`   | inequality               |left          | compatible | `Bool`
| `e1 < e2`    | less than                |left          | compatible | `Bool`
| `e1 \<= e2`  | less than or equal to    |left          | compatible | `Bool`
| `e1 > e2`    | greater than             |left          | compatible | `Bool`
| `e1 >= e2`   | greater than or equal to |left          | compatible | `Bool`
| `e1 + e2`    | concatenation            |left          | `String`   | `String`
| `e1 + e2`    | addition                 |left          | number     | number
| `e1 - e2`    | subtraction              |left          | number     | number
| `e1 * e2`    | multiplication           |left          | number     | number
| `e1 / e2`    | division                 |left          | number     | `Rat` or `Float`
| `e1 % e2`    | modulo                   |left          | number     | number
| `!e`         | logical negation         |right         | `Bool`     | `Bool`
| `-e`         | integer negation         |right         | number     | number
|=======================


==== Semantics of Comparison Operators

ABS has generic equality and less-than comparison between two values of the
same type.

Equality and inequality comparison is standard: by value for functional
datatypes and by reference for objects and futures.  I.e., two strings
`"Hello"` compare as identical via `==`, as do two sets containing identical
values.  Two references to objects or futures compare as identical via `==` if
they point to the same object or future.  The inequality operator `!=`
evaluates to `True` for any two values that compare to `False` under `==` and
vice versa.

For the comparison operator `<`, an ordering is defined in the following way.

- Numbers compare as usual.

- Strings compare lexicographically.

- Algebraic datatypes compare first by constructor name, then by comparing
  constructor arguments left to right.

.Example
----
Cons(_, _) < Nil
Cons(1, _) < Cons(2, _)
----

- Objects and futures are compared by identity, in an implementation-specific
  but stable way.  This means that for any two variables `a` and `b` that
  point to different objects, the value of `a < b` does not change as long as
  `a` and `b` are not re-assigned.footnote:[This ordering is not guaranteed to
  be stable between two invocations of a program.  If ABS ever develops object
  serialization, care must be taken to uphold any datatype invariants across
  program invocations, e.g., when reading back an ordered list of objects.]


=== Let

The expression `let T v = p in b` evaluates `b`, with `v` bound to the value
of evaluating the expression `p`.  The newly-introduced binding of `v` can
shadow a binding of `v` outside of the `let` expression.

More than one binding can be established in one `let` expression.
Bindings are evaluated sequentially, i.e., later bindings can use
earlier variables in their value expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
|_LetExp_ ::= | `let` _Type_ _SimpleIdentifier_ `=` _PureExp_ +
{ `,` _Type_ _SimpleIdentifier_ `=` _PureExp_ } +
`in` _PureExp_
|====

[source]
.Example
----
let Int x = 2 + 2, Int y = x + 1 in y * 2 <1>
----
<1> The value of this expression is 10 (`((2 + 2) + 1) * 2`)

=== Data Constructor Expressions


Data Constructor Expressions are expressions that create data values by
applying arguments to data type constructors.  Data constructor expressions
look similar to function calls, but data constructors always start with an
upper-case letter.

For data type constructors without parameters, the parentheses are optional.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _DataConstrExp_ ::= | _TypeIdentifier_ [ `(` [ _PureExp_ { `,` _PureExp_ } ] `)` ]
|====

[source]
.Example
----
True
Cons(True, Nil)
Nil
----

Defining new data types and their constructors is described in
<<sec:algebraic-data-types>>.


=== Function Calls

Function calls apply arguments to functions, producing a value.  Function call
expressions look similar to data constructor expressions, but function names
always start with a lower-case letter.  The parentheses are mandatory in
function calls.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _FnAppExp_ ::= | _Identifier_ `(` [ _PureExp_ { `,` _PureExp_ } ] `)`
|====

[source]
.Example
----
tail(Cons(True, Nil))
head(list)
----


==== N-ary Function Calls

Calls to n-ary Constructors (see <<sec:n_ary-constructors>>) are written with
brackets (`[]`) instead of parentheses (`()`).

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _FnAppListExp_ ::= | _Identifier_ `[` [ _PureExp_ { `,` _PureExp_ } ] `]`
|====


=== Partially-Defined-Function Calls

Calls to partially defined functions (see <<sec:partially-defined-functions>>) are similar to
function call expressions, but have an additional prepended set of arguments.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _ParFnAppExp_ ::= | _Identifier_ +
  `(` _ParFnAppParam_ { `,` _ParFnAppParam_ } `)` +
  `(` [ _PureExp_ { `,` _PureExp_ } ] `)`

|  _ParFnAppParam_ ::= | _Identifier_ +
                  {vbar} _AnonymousFunction_

|  _AnonymousFunction_ ::= | `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_  } ]  `)` `+++=>+++` _PureExp_
|====

[source]
.Example
----
map(toString)(list[1, 2])
filter((Int i) => i > 0)(list[0, 1, 2])
----


=== Conditional Expressions

The value of the conditional expression `when c then e1 else e2` is either the
value of `e1` or the value of `e2`, depending on the value of `c`, which must
be of type `Bool`.  Depending on the value of `c`, either `e1` or `e2` is
evaluated, but not both.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _WhenExp_ ::= | `when` _PureExp_ `then` _PureExp_ `else` _PureExp_
|====

[source]
.Example
----
when 5 == 4 then True else False
----

[[case-expression]]
=== Case

ABS supports pattern matching via the Case Expression.  A case expression
consists of an input expression and one or more branches, each consisting of a
pattern and a right hand side expression.

The case expression evaluates its input expression and attempts to match the
resulting value against the branches until a matching pattern is found.  The
value of the case expression itself is the value of the expression on the
right-hand side of the first matching pattern.

If no pattern matches the expression, a `PatternMatchFailException` is thrown.

There are four different kinds of patterns available in ABS:

* Variables (with different semantics depending on whether the variable is bound or not)
* Literal Patterns (e.g., `5`)
* Data Constructor Patterns (e.g., `Cons(Nil,x)`)
* Underscore Pattern (`_`)

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _CaseExp_ ::= | `case` _PureExp_ `{` _CaseExpBranch_ { `|` _CaseExpBranch_ } `}`
| _CaseExpBranch_ ::=  | _Pattern_ `\=>` _PureExp_
| _Pattern_ ::= | `_` +
                 {vbar} __SimpleIdentifier__ +
                 {vbar} __Literal__ +
                 {vbar} __ConstrPattern__
| _ConstrPattern_ ::= | _TypeIdentifier_ [ `(` [ _Pattern_ { `,` _Pattern_ }  ] `)` ]
|====

NOTE: Older versions of the ABS language used the semicolon (`’`) to terminate
case branches.  This older syntax is still supported by the compiler.

==== Variable Patterns

Variable patterns are written as identifiers starting with a lower-case
letter.  If the identifier does not name a variable in the current scope, the
variable pattern matches any value and introduces a binding of the given
identifier to the matched value for the right-hand side of the branch and the
rest of the pattern itself.  In case a binding for that identifier is already
in scope, its value is compared to the value being matched against.

The variable being named by the variable pattern can be used in the
right-hand-side expression of the corresponding branch.  Typically, pattern
variables are used inside of data constructor patterns to extract values from
data constructors.  For example:


[source]
.Example
----
let (Pair<Int, Int> a) = Pair(5, 5) in
  case a {
    Pair(x, x) => x <1>
    | Pair(x, y) => y <2>
  } <3>
----
<1> This branch matches a pair with identical values.
<2> This branch matches every pair.  Since pairs with identical values are matched by the previous branch, `x` and `y` will be different.
<3> The value of the whole expression is 5, produced by the first branch.


[source]
.Example
----
let (x = 7) in
  case Pair(5, 5) {
    Pair(x, x) => x <1>
    | Pair(x, y) => y <2>
    | Pair(y, z) => z <3>
  } <4>
----
<1> This pattern does not match since `x` is bound to 7 and does not match 5.
<2> This pattern does not match either, for the same reason.
<3> This pattern contains only unbound variable patterns and therefore matches.
<4> The value of the whole expression is 5, produced by the third branch.



==== Literal Patterns

Literals can be used as patterns.  The pattern matches if the value of the
case expression is equal to the literal value.

[source]
.Example
----
let (Pair<Int, Int> a) = Pair(5, 5) in
  case a {
    Pair(3, x) => x <1>
    | Pair(x, y) => y <2>
  } <3>
----
<1> The pattern `3` does not match the value in the first position of the `Pair` constructor pattern.
<2> This pattern matches.
<3> The value of the whole expression is 5, produced by the second branch.


==== Data Constructor Patterns

A data constructor pattern is written like a standard data constructor expression.
Constructor arguments are again patterns.


[source]
.Example
----
let (List<Int> l) = list[1, 2, 3] in
  case l {
    Nil => 0 <1>
    | Cons(1, _) => 15 <2>
    | Cons(_, Cons(y, _)) => y <3>
  } <4>
----
<1> This pattern matches the empty list.
<2> This pattern matches a list starting with the literal `1`.
<3> This pattern matches a list of at least length 2, and binds the second element to `y`.
<4> The value of the whole expression is 15, produced by the second branch.


==== The Wildcard Pattern

The wildcard pattern, written with an underscore (`_`) matches any value.

[source]
.Example
----
let (List<Int> l) = list[1, 2, 3] in
  case l {
    Nil => True <1>
    | _ => False <2>
}; <3>
----
<1> This pattern matches the empty list.
<2> This pattern matches anything.
<3> The value of the whole expression is `False`, produced by the second branch.

The wildcard pattern can be used as the last pattern in a case expression to
define a default case.


.Typing of Case Expressions

A case expression is type-correct if and only if all its expressions and all
its branches are type-correct and the right-hand side of all branches have a
common super type.  This common super type is also the type of the overall case
expression.  A branch (a pattern and its expression) is type-correct if its
pattern and its right-hand side expression are type-correct.  A pattern is
type-correct if it can match the corresponding case expression.



[[typecheck-expression]]
=== Type-Check Expressions

Variables pointing to objects are typed by interface, which means that the
concrete class of the referenced object might support more methods than can be
called through the reference.  The type-check expression checks if an object
implements the given interface.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _TypeCheckExp_ ::= | _PureExp_ `implements` _TypeIdentifier_
|====

[source]
.Example
----
interface I {}
interface J {}
class C implements I, J {}
{
  I o = new C();
  if (o implements J) {  // evaluates to True
    println("o is a J");
  }
}
----




[[typecast-expression]]
=== Type-Cast Expressions

Variables pointing to objects are typed by interface, which means that
the concrete class of the referenced object might support more methods
than can be called through the reference.  The type-cast expression
returns a reference of type `I` to the same object if it implements
the given interface `I`, or `null` otherwise.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _TypeCastExp_ ::= | _PureExp_ `as` _TypeIdentifier_
|====

[source]
.Example
----
interface I {}
interface J {}
class C implements I, J {}
class D implements I {}
{
  I o = new C();
  J j = o as J;  // j is an alias of o, with type J
  I o2 = new D();
  J j2 = o2 as J; // j2 is null
}
----

=== New

A `new` expression creates a new object from a class name and a list of
arguments.  In ABS objects can be created in two different ways.  Either they
are created in the current COG, using the `new local` expression, or they are
created in a new COG by using the `new` expression (see
<<sec:concurrency-model>> for more details about cogs).

NOTE: This expression is a side-effect expression and cannot be used as a sub-expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _NewExp_ ::= | `new` [ `local` ] _TypeIdentifier_ `(` [ _PureExp_ {`,` _PureExp_ } ] `)`
|====

[source]
.Example
----
new local Foo(5)
new Bar()
----

Classes can declare an _init block_ (see <<sec:classes>>), which is executed for
each new instance.  The semantics of the `new` expression guarantee that the
init block is fully executed before the new object begins receiving method
calls.  Classes can also declare a `run` method, which is asynchronously
invoked after the init block and subject to the normal scheduling rules for
processes.

CAUTION: When the fresh object gets passed `this` as argument, it is
possible to execute synchronous calls to the creating object in the
init block while the creating task is executing the `new` or `new
local` expression.  This works as expected when the fresh object is on
the same cog (i.e., is created with `new local` but will deadlock when
the fresh object is on its own cog).

=== Synchronous Method Calls

A synchronous call consists of a target expression evaluating to an interface
type, a method name declared in that interface, and a list of argument expressions.

NOTE: This expression is a side-effect expression and cannot be used as a sub-expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _SyncCall_ ::= | _PureExp_ `.` _SimpleIdentifier_ `(` [ _PureExp_ { `,` _PureExp_ } ] `)`
|====

[source]
.Example
----
Bool b = x.m(5, 3);
----

The semantics of the synchronous method call differ depending on whether the
caller and callee are in the same cog.  A synchronous method call between
objects in the same cog has Java-like semantics, i.e., the caller is suspended
and the called method starts executing immediately.  When the called method
finishes, the caller process is scheduled and resumes execution.

In the case when caller and called object are in different cogs, a synchronous
method call is equivalent to and asynchronous method call immediately followed
by a `get` expression on the resulting future.  This means that the intuitive
semantics of synchronous method calls are preserved, but introduces the
possibility of deadlocks in case the callee tries to call back to an object of
the caller cog.


[[async-call-expression]]
=== Asynchronous Method Calls

An asynchronous call consists of a target expression evaluating to an
interface type, a method name declared in that interface, and a list of
argument expressions.

NOTE: This expression is a side-effect expression and cannot be used as a sub-expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _AsyncCall_ ::= | _PureExp_ `!` _SimpleIdentifier_ `(` [ _PureExp_ { `,` _PureExp_ } ] `)`
|====

An asynchronous method call creates a new task in the COG that contains the
target.  This means that the caller task proceeds independently and in
parallel with the callee task, without waiting for the result.  The result of
evaluating an asynchronous method call expression `o!m(e)` is a _future_ of
type (`Fut<V>`), where `V` is the return type of the callee method `m`.

This future is resolved (i.e., it gets a value) when the callee task finishes.
It can be used to synchronize with the callee task and obtain the result of
the method call.

[source]
.Example
----
Fut<Bool> f = x!m(5);
----

[[get-expression]]
=== Get

A get expression is used to obtain the value from a future.  The current task
is blocked until the future has been resolved, i.e., until either the return
value is available or an exception has occurred in the callee task.  No other
task in the COG can be activated while the current task is blocked by a get
expression.

NOTE: This expression is a side-effect expression and cannot be used as a sub-expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _GetExp_ ::= | _PureExp_ `.` `get`
|====

[source]
.Example
----
Bool b = f.get;
----

If the future contains a normal return value, the value of the get expression
is that value.  If the future contains an exception thrown by the callee
process, evaluating the get expression will throw the same exception.  The
value thrown by a get expression can be caught by try-catch as normal (see
<<try-catch-finally-stmt>>).

The following example assigns the return value contained in `f` to the
variable `b`.  In case of any error, `b` is assigned `False`.

[source]
.Example
----
try b = f.get; catch { _ => b = False; }
----


[[await-expression]]
=== Await (Expression)

An await expression is a way to asynchronously call a method, wait for the
callee to finish, and optionally get the result in one expression.

NOTE: This expression is a side-effect expression and cannot be used as a sub-expression.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _AwaitExp_ ::= | `await` _AsyncCall_
|====

[source]
.Example
----
A x = await o!m();
----

The statement above is equivalent to these three statements:

[source]
.Example
----
Fut<A> fx = o!m();
await fx?;
A x = fx.get;
----

==== Exception Propagation and the Await Expression

As explained in Section <<get-expression>>, exceptions propagate from callee
to caller via the `get` expression.  An `await` statement will proceed once
the callee process has finished, but an exception in the future will not be
raised when executing the `await` statement.  To align the `await` expression
with that behavior, an exception will only be raised when the return value of
a method call is used, e.g., by assigning it to a variable. Hence the
following line of code will not raise an error even if the call to `o!m()`
results in an exception:

[source]
.Example
----
await o!m();
----

Since the return value is ignored in the statement above, it is equivalent to
these two statements:

[source]
.Example
----
Fut<A> fx = o!m();
await fx?;
----



== Function Definitions

Functions take a list of arguments and evaluate the expression in their body,
producing a return value.  ABS functions are always pure.  This means the body
of a function can use all pure expressions but
no expressions with side effects.


Functions can be _parametric_, which means that they can take and return
parametric datatypes.  This means that a function `head` defined over a
parametric list datatype can return the first element of a list, regardless of
its type.  Parametric functions are defined like normal functions but have an
additional type parameter section inside angle brackets (`<` `>`) after the
function name.


[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _FunctionDecl_  ::= | `def` _Type_ _SimpleIdentifier_ [ `<` _SimpleTypeIdentifier_ { `,` _SimpleTypeIdentifier_ } `>` ] +
                        `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_  } ]  `)` +
                        `=` _PureExp_ `;`
|====



[source]
.Example
----
def Rat abs(Rat x) = if x > 0 then x else -x; <1>

def Int length<A>(List<A> list) = <2>
case list {
  Nil => 0
  | Cons(_, ls) => 1 + length(ls)
};

def A head<A>(List<A> list) = <3>
  case list { Cons(x, _) => x };
----
<1> The `abs` function returns the absolute value of its argument.
<2> This parametric function takes lists with arbitrary values and returns an Integer.
<3> This parametric function returns the same type that is contained in the list.  (Note that `head` is a partial function which is not defined for empty lists.)

[[sec:partially-defined-functions]]
=== Partial Function Definitions

For reasons of simplicity and analyzability, ABS does not offer higher-order
functions.  On the other hand, many common patterns of functional programming
are extremely useful, for example the well-known `map`, `filter` and `fold`
higher-order functions.  For this reason, ABS supports _partial function
definitions_.

Partial function definitions are function definitions taking an additional set
of parameters.  These additional parameters can be either names of normal
functions, or anonymous functions (see <<sec:anonymous-functions>>).  Partial
function definitions define a set of functions which only differ in function
applications but share overall structure.  Put another way, partial function
definitions define second-order functions -- functions that take first-order
functions as arguments.  Partially defined functions can be used inside
functional code, but cannot be passed as parameters to other partial
functions.

A partially defined function is called the same way as a normal function, with
a separate, non-empty argument list containing the functional arguments.  For recursion
inside the body of a partially defined function, omit the function parameter
list.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _PartialFunctionDecl_  ::= | `def` _Type_ _SimpleIdentifier_ [ `<` _SimpleTypeIdentifier_ { `,` _SimpleTypeIdentifier_ } `>` ] +
                        `(` _SimpleIdentifier_ { `,` _SimpleIdentifier_  } `)` +
                        `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_  } ]  `)` +
                        `=` _PureExp_ `;`
|====

[source]
.Example
----
// Simply applies a function fn to a value.
def B apply<A, B>(fn)(A value) = fn(a);

def Int double(Int x) = x * 2;

{
  // doubled = 4
  Int doubled = apply(double)(2);
}
----

[source]
.Example
----
def List<B> map<A, B>(f)(List<A> list) = case list { <1>
    Nil => Nil
    | Cons(x, xs) => Cons(f(x), map(xs)) <2>
};

def Int double(Int x) = x * 2;

{
  // doubled = [2, 4, 6]
  List<Int> doubled = map(double)(list[1, 2, 3]);
}
----
<1> This definition of `map` is contained in the standard library.
<2> Note the recursive call to `map` omits the function parameter list.

NOTE: For each call of a partial function, a normal function definition is
generated at compile time by replacing the functional parameters syntactically
by the functions passed in the additional parameter list.  This is done before
type checking and after delta and trait flattening -- any type mismatch and
similar errors are caught afterwards during type checking.  If multiple
statements call a partially defined function with the same function-name
arguments, only one expansion is generated.


[[sec:anonymous-functions]]
=== Anonymous Functions

To reduce the need to declare a function with a new function name explicitly
every time a partially defined function is called, ABS uses anonymous
functions.  Anonymous functions are only allowed in the first arguments list
calls to partially defined functions.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _AnonymousFunction_  ::= | `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_  } ]  `)` `=>` _PureExp_
|====

An anonymous function specifies a number of parameters and an expression that
may refer to the declared parameters.

The following example is equivalent to the previous example, but does not
define the `double` function explicitly.

[source]
.Example
----
{
  List<Int> list = list[1, 2, 3];
  list = map((Int y) => y * 2)(list);
}
----

Anonymous functions can refer to variables and fields accessible in the
context of the partial function call.  (Since anonymous functions are not
first-class values, no closure is created.)

[source]
.Example
----
{
  Int factor = 5;
  List<Int> list = list[1, 2, 3];
  list = map((Int y) => y * factor)(list);
  // list = [5, 10, 15]
}
----

NOTE: Anonymous functions are inlined into the expansion of the partial
function definition.  Errors caused by wrong typing are caught after the
expansion during the type checking of core ABS, but the expanded function
definition has an annotation referring to the statement that caused the
expansion, hence error reporting will be accurate wrt. the original source
code.

=== Built-In Functions

Some special "functions" cannot be defined with pure expressions, for example
the function `println`.  The definition of such functions is done via a
special function body written as `builtin`, with optional pure expression
arguments `builtin(a, "b", 3)`.  Such builtin functions have to be defined
separately in the code generator for each backend where the model is compiled.

Built-in functions in the standard library include:

- The functions `sqrt`, `log`, `exp` that work on floating-point numbers
- Functions that convert between different numerical types: `truncate`, `float`, `rat`, `floor`, `ceil`, `numerator`, `denominator`
- String functions: `substr`, `strlen`, `toString`, `println`
- Clock access: `currentms`, `ms_since_model_start`
- Functions that return process attributes (see Section <<sec:process-attributes>>)
- The `random` function

=== Embedded SQLite Database Queries

The Erlang backend can read from a relational database and convert the
result into ABS lists.  Currently only SQLite databases are supported.

A SQLite database is queried by writing a function with a `builtin`
body with three arguments: a literal `sqlite3`, the name of the
database file, and the query as a SQL string.  The return value is a
list of ABS values.  The return type of such a function can be:

- `Int`, when the query returns rows with a single SQLite `INTEGER`
  value;
- `Float`, when the query returns rows with a single SQLite `INTEGER`
  or `REAL` value;
- `Rat`, when the query returns rows with a single SQLite `INTEGER` or
  `REAL` value;
- `Bool`, when the query returns rows with a single SQLite `INTEGER`
  value, where `0` corresponds to ABS `False`;
- `String`, when the query returns rows with a single SQLite `TEXT`
  value;
- An algebraic datatype, with a single constructor taking only the
  above datatypes, such that the constructor can be invoked for each
  row returned by the query.

[source]
.Example: creating a database
----
$ sqlite3 /tmp/test.sqlite3
CREATE TABLE IF NOT EXISTS test_table (
  int_value INTEGER,
  float_value REAL,
  string_value TEXT,
  bool_value BOOLEAN
);
INSERT INTO test_table(int_value, float_value, string_value, bool_value) VALUES(15, 13.53, "hello", 0);
INSERT INTO test_table(int_value, float_value, string_value, bool_value) VALUES(30, 42.5, "world", 1);
.quit
----

With the above database, the following ABS model can be run:

[source]
.Example: reading from the database
----
module Test;

def List<String> fstring() = builtin(sqlite3, <1>
    "/tmp/test.sqlite3",
    "SELECT string_value FROM test_table");

def List<Rat> frat() = builtin(sqlite3, <2>
    "/tmp/test.sqlite3",
    "SELECT float_value FROM test_table");

data RowResult = RowResult(Int, Bool, Float, Rat, String);

def List<RowResult> ftuple() = builtin(sqlite3, <3>
    "/tmp/test.sqlite3",
    `SELECT int_value, bool_value, float_value, float_value, string_value
     FROM test_table`);

{
    foreach (v, i in frat()) {
        println(`$i$'th rational value is $v$`);
    }
    foreach (v, i in fstring()) {
        println(`$i$'th string value is $v$`);
    }
    foreach (v, i in ftuple()) {
        println(`$i$'th tuple is $v$`);
    }
}
----
<1> A `builtin` function with first argument `sqlite3` takes two more arguments: the name of the database, and a SQL query string.
<2> SQL float values are converted to ABS rational values
<3> Rows of SQL relations with more than one column are converted into ABS user-defined datatypes. The data constructor of the result datatype has to take the same number of arguments as the `SELECT` returns, and the datatypes must be compatible.

NOTE: When the database file (the second argument to the `builtin` expression)
does not contain a path, the model will look for it in the `priv` directory of
the compiled model.  That directory is the value of the erlang function
`code:priv_dir(absmodel)`, typically
`gen/erl/absmodel/_build/default/lib/absmodel/priv/`.

NOTE: The SQL query currently cannot contain parameters; this will be
implemented in a future release.
