[[sec:interfaces]]
== Interfaces


Interfaces in ABS are similar to interfaces in Java.  They have a name, which
defines a nominal type, and they can extend zero or more other
interfaces.  The interface body consists of a list of method signature
declarations.  Method names start with a lowercase letter.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _InterfaceDecl_ ::= | `interface` _SimpleTypeIdentifier_ [ `extends` _InterfaceList_ ] `{` { MethSig } `}`
| _InterfaceList_ ::= | _TypeIdentifier_ { `,` _TypeIdentifier_ }
| _MethSig_ ::= | _Type_ _SimpleIdentifier_ `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ } ] `)` `;`
|====

The interfaces in the example below represent a database system, providing
functionality to store and retrieve files, and a node of a peer-to-peer file
sharing system.  Each node of a peer-to-peer system plays both the role of a
server and a client.

[source]
.Example
----
interface DB {
  File getFile(Filename fId);
  Int getLength(Filename fId);
  Unit storeFile(Filename fId, File file);
  Filenames listFiles();
}
interface Client {
  List<Pair<Server,Filenames>> availFiles(List<Server> sList);

  Unit reqFile(Server sId, Filename fId);
}
interface Server {
  Filenames inquire();
  Int getLength(Filename fId);
  Packet getPack(Filename fId, Int pNbr);
}
interface Peer extends Client, Server {
  List<Server> getNeighbors();
}
----

[[sec:classes]]
== Classes

Classes in ABS are used to create objects via the `new` expression.  Classes
can implement an arbitrary number of interfaces.  Since classes are not types
in ABS, classes typically implement one or more interfaces.


Classes in ABS have zero or more _class parameters_.  Each class parameter
defines a field of the class which is assigned a value via arguments the `new`
expression.


Classes have an optional init block, which is executed before any other code.
The init block cannot contain processor release points (i.e., `await` or
`suspend`), blocking expressions (i.e., `get`), or explicitly throw an
exception via the `throw` statement.

Classes have an optional recovery block.  In case an uncaught exception occurs
in a method, the exception is matched against the patterns given in the
recovery block, and the associated statement(s) are executed.  If the
exception does not match any pattern in the recovery block, or if the recovery
block itself raises an exception, the object is killed.  Code in the recovery
block has the same restrictions as in the init block.


[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _ClassDecl_ ::= | `class` _SimpleTypeIdentifier_ [ `(` [ _ClassParameterList_ ] `)` ] [ `implements` _InterfaceList_ ]
|                 | `{` [ _FieldDeclList_ ] [ _Block_ ] [_RecoveryBlock_] [ _TraitUseList_ ][ _MethDeclList_ ] `}`
| _ClassParameterList_ ::= | _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ }
| _InterfaceList_ ::= | _TypeIdentifier_ { `,` _TypeIdentifier_ }
| _TraitUseList_ ::= | adds _TraitName_ `;` {  adds _TraitName_ `;`}
| _FieldDeclList_ ::= | { _Type_ _SimpleIdentifier_ [ `=` _PureExp_ ] `;` }
| _RecoveryBlock_ ::= | `recover` `{` { _Pattern_ `\=>` _Stmt_ } `}`
| _MethDeclList_ ::= | _Type_ _SimpleIdentifier_ `(` [ _Type_ _SimpleIdentifier_ { `,` _Type_ _SimpleIdentifier_ } ] `)` _Block_
|====





[source]
.Example
----
class DataBase(Map<Filename,File> db) implements DB {
	File getFile(Filename fId) {
		return lookup(db, fId);
	}

	Int getLength(Filename fId){
		return length(lookup(db, fId));
	}

	Unit storeFile(Filename fId, File file) {
		db = insert(Pair(fId,file), db);
	}

	Filenames listFiles() {
		return keys(db);
	}

	}

class Node(DB db, Peer admin, Filename file) implements Peer {

	Catalog catalog;
	List<Server> myNeighbors;
	// implementation...

}

----

.Active Classes

A class can be active or passive. Active classes start an activity on their own upon creation. Passive classes only react to incoming method calls. A class is active if and only if it has a run method:

[source]
----
Unit run() {
	// active behavior ...
}
----

The run method is called after object initialization.

[[sec:traits]]
== Traits

ABS does not support inheritance for code reuse.  Method implementations that
are common betwen classes can be defined once and used inside these classes by
using _traits_.  A trait can add, remove and modify methods of a class or of
another trait.

Traits are applied to classes at compile-time and do not change the
interface(s) of a class.  Classes and their methods are type-checked once all
traits are applied.

Similar to classes, traits in ABS are not types.

[frame=topbot, options="noheader", grid=none, caption="", cols=">30,<70"]
.Syntax
|====
| _TraitDecl_ ::= | `trait` _TraitName_ = ( `{` _MethDeclList_ `}` {vbar} _TraitName_ ) _TraiOper_*
| _TraitName_ ::= | _SimpleIdentifier_
| _TraitOper_ ::= | `adds` _TraitExpr_ {vbar} `modifies` _TraitExpr_ {vbar} `removes` _MethSig_+
|====


A trait is defined with `trait` t = _T_ at module level.

The effect of applying a trait _T_ to a class (using `adds` _T_ inside the
class body) is to add the methods comprising that trait to the class
definition.

.Explanation
* The operation `adds` adds all the elements of the next _MethodSet_ to the class. 
If a method with the same name is already present in the class (or set of methods), the error will be raised _after_ applying all traits, during type checking.
[source]
----
trait T = { Unit x(){ skip; } } 
trait T2 = { Unit y(){ skip; } } adds T
----
will be resolved to the set
[source]
----
{ Unit x(){ skip; } Unit y(){ skip; } } 
----
* The operation `modifies` changes all the elements of the next _MethodSet_ in the class to the new implementation described in this _MethodSet_. 
A trait may contain original() calls which refer to the version of the method before the trait application.
If a method with the same name is not present in the class (or set of methods), the method is added instead and the original() calls are not resolved.

A method may contain targeted original calls. These raise an error if the trait is used directly by a class
The following is invalid:
[source]
.Example
----
trait T = {Unit myMethod(){ skip; }}  modifies {Unit myMethod(){ core.original(); }}
class C {uses T; }
----
The following two examples are valid:
[source]
.Example
----
trait T = {Unit myMethod(){ skip; }} modifies {Unit myMethod(){ original(); }}
class C {uses T; }
----
[source]
.Example
----
module M;
trait T = {Unit myMethod(){ skip; }} 
class C {uses T; }

delta D;
modifies class M.C{
        modifies Unit myMethod(){ core.original(); }
}
----
* The operation `removes` deletes the method with the provided signature. 
If a method with the same name is not present in the class (or set of methods), an error will be raised during trait application.

.The order of trait application is as follows:
* All traits used within a class, in the order they are refered too
* All traits used within a delta, in the order they are refered too

[source]
.Example
----
module M;
interface I { Unit x(); Unit foo(); Unit bar(); }
trait T = Unit x() { this.foo(); original(); this.bar();  }
trait T2 = { Unit x() { println("T2"); } } modifies T
trait T3 = { Unit x() { println("T3"); } } modifies T
class C implements I {
        Int i = 0;
        uses T2;
        Unit foo(){ i = i+1; }
        Unit bar(){ i = i-1; }
}

class C2 implements I {
        Int i = 0;
        uses T3;
        Unit foo(){ i = i-1; }
        Unit bar(){ i = i+1; }
}
----
